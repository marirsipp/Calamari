!   Commands inserted into this file will be executed just prior to the ANSYS SOLVE command.
!   These commands may supersede command settings set by Workbench.

!   Active UNIT system in Workbench when this object was created:  Metric (mm, kg, N, s, mV, mA)
!   NOTE:  Any data that requires units (such as mass) is assumed to be in the consistent solver unit system.
!                See Solving Units in the help system for more information.

*DIM,path0,STRING,100
*DIM,path1,STRING,100

path0(1) = ('D:\Bingbin\Models\@Round5_WFA\Itr10_Col1Top_Mod2\Results') !! General path

ItrNo = 'Itr10_1'                !! Current iteration 
LdCs = 'Fx'                      !! Current load case name

path1(1) = '%path0(1)%\%ItrNo%'       !! Result file path name

/SYP,mkdir,path1(1)                   !! Create folder for result output
------------------------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------------------------

!   Commands inserted into this file will be executed immediately after the ANSYS /POST1 command.

!   Active UNIT system in Workbench when this object was created:  Metric (mm, kg, N, s, mV, mA)
!   NOTE:  Any data that requires units (such as mass) is assumed to be in the consistent solver unit system.
!                See Solving Units in the help system for more information.


FINISH 
/post1 
set,last                        !! result set to be used

!-------------------Input----------------------------------------------------------------
Cnnt = 'L1'                     !! Connection name (your choice)
Part = 'TB'                     !! Name of parts where stresses are read (your choice)
LocSys = 12                     !! node location coordinate system
SolSys = 12                     !! solution coordinate system            
CMSEL, S, OUT_L1_TB             !! select the nodes at which results are to be exported
                                !! OUT_L1_TB need to match the name of the Named Selection for  
                                !! the edge of connection TB*TF
!----------------------------------------------------------------------------------------

*get,nnum ,Node,,Count          !! get number of selected nodes 
*dim,StrOut,Array,nnum,16       !! create an array 
a = 0                           !! define a parameter

!-------------------Input---------------------------------------------------------------- 
CMSEL,S,RST_TB_C1,ELEM          !! select elements for stress results, RST_TB_C1 need to 
                                !! match the name of the Named Selection for body TB 
!----------------------------------------------------------------------------------------

*do,i,1,nnum                    !! initiate a loop on selected nodes 
 a = NDNEXT(a)                  !! get the next higher node number in selected set 
 StrOut(i,1) = a                !! store the node number in first column of array  

 csys, LocSys                   !! set node location coordinate system to LocSys

 *get,xloc,Node,a,loc,x         !! get x coordinate of node
 *get,yloc,Node,a,loc,y         !! get y coordinate of node
 *get,zloc,Node,a,loc,z         !! get z coordinate of node

 rsys, SolSys                   !! set stress coordinate system to SolSys

 shell, top                     !! Specify output layer, top,bot,mid

 *get,StrXt,Node,a,s,x          !! get Sxx of node
 *get,StrYt,Node,a,s,y          !! get Syy of node
 *get,StrZt,Node,a,s,z          !! get Szz of node

 *get,StrXYt,Node,a,s,xy        !! get Sxy of node
 *get,StrYZt,Node,a,s,yz        !! get Syz of node
 *get,StrXZt,Node,a,s,xz        !! get Sxz of node

 shell, bot                     !! Specify output layer, top,bot,mid

 *get,StrXb,Node,a,s,x          !! get Sxx of node
 *get,StrYb,Node,a,s,y          !! get Syy of node
 *get,StrZb,Node,a,s,z          !! get Szz of node

 *get,StrXYb,Node,a,s,xy        !! get Sxy of node
 *get,StrYZb,Node,a,s,yz        !! get Syz of node
 *get,StrXZb,Node,a,s,xz        !! get Sxz of node


 StrOut(i,2) = xloc             !! store the x coordinate in second column of array
 StrOut(i,3) = yloc             !! store the y coordinate in third column of array
 StrOut(i,4) = zloc             !! store the z coordinate in fourth column of array

 StrOut(i,5) = StrXt            !! store the x stress in fifth column of array
 StrOut(i,6) = StrYt            !! store the y stress in sixth column of array
 StrOut(i,7) = StrZt            !! store the z stress in seventh column of array

 StrOut(i,8) = StrXYt           !! store the xy stress in eighth column of array
 StrOut(i,9) = StrYZt           !! store the yz stress in ninth column of array
 StrOut(i,10) = StrXZt          !! store the xz stress in tenth column of array

 StrOut(i,11) = StrXb           !! store the x stress in fifth column of array
 StrOut(i,12) = StrYb           !! store the y stress in sixth column of array
 StrOut(i,13) = StrZb           !! store the z stress in seventh column of array

 StrOut(i,14) = StrXYb          !! store the xy stress in eighth column of array
 StrOut(i,15) = StrYZb          !! store the yz stress in ninth column of array
 StrOut(i,16) = StrXZb          !! store the xz stress in tenth column of array

*enddo                          !! end the loop

*DIM,OutFile1,STRING,150        !! Output file name1 
*DIM,OutFile2,STRING,150        !! Output file name2 

OutFile1(1) = '%path1(1)%\%Cnnt%_%Part%_Top_%LdCs%'   !!Output file name include connection name, part name, top or bot surface and load case name

*cfopen, %OutFile1(1)%,csv      !! create a csv file, then write header, define header format, write stress result, define result format
*Vwrite,'Node_num',',','X-cord',',','Y-cord',',','Z-cord',',','X-str-t',',','Y-str-t',',','Z-str-t',',','XY-str-t',',','YZ-str-t',',','XZ-str-t'
(A8,A1,A6,A1,A6,A1,A6,A1,A7,A1,A7,A1,A7,A1,A8,A1,A8,A1,A8)
*Vwrite,StrOut(1,1),',',StrOut(1,2),',',StrOut(1,3),',',StrOut(1,4),',',StrOut(1,5),',',StrOut(1,6),',',StrOut(1,7),',',StrOut(1,8),',',StrOut(1,9),',',StrOut(1,10)
(F15.3,A1,f15.3,A1,f15.3,A1,f15.3,A1,E15.3,A1,E15.3,A1,E15.3,A1,E15.3,A1,E15.3,A1,E15.3) 
*cfclose  !! close the csv file after writing the required data

OutFile2(1) = '%path1(1)%\%Cnnt%_%Part%_Btm_%LdCs%'

*cfopen, %OutFile2(1)%,csv      !! create a csv file
*Vwrite,'Node_num',',','X-cord',',','Y-cord',',','Z-cord',',','X-str-b',',','Y-str-b',',','Z-str-b',',','XY-str-b',',','YZ-str-b',',','XZ-str-b'
(A8,A1,A6,A1,A6,A1,A6,A1,A7,A1,A7,A1,A7,A1,A8,A1,A8,A1,A8)
*Vwrite,StrOut(1,1),',',StrOut(1,2),',',StrOut(1,3),',',StrOut(1,4),',',StrOut(1,11),',',StrOut(1,12),',',StrOut(1,13),',',StrOut(1,14),',',StrOut(1,15),',',StrOut(1,16)
(F15.3,A1,f15.3,A1,f15.3,A1,f15.3,A1,E15.3,A1,E15.3,A1,E15.3,A1,E15.3,A1,E15.3,A1,E15.3) 
*cfclose  !! close the csv file after writing the required data

fini
